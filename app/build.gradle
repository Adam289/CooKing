apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.adam289.cooking"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            //移除无用的资源文件
            shrinkResources true
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //输出APK路径
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为厨王_v1.0_2015-01-15_渠道.apk
                        def fileName = "厨王_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    //多渠道打包
    productFlavors {
        ATest{}
        Fir{}
        Tencent {}
        Qh360 {}
        WangDouJia {}
        XiaoMi {}
        BaiDu {}
        Huawei {}
    }

    //多渠道打包
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [APK_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:support-vector-drawable:25.2.0'
    testCompile 'junit:junit:4.12'

    // 数据库
    compile 'org.litepal.android:core:1.5.0'
    // 依赖注入
    compile 'com.jakewharton:butterknife:8.5.1'
    //友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'
    //Rxjava
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    //TODO 需升级新版本了
    //compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //compile 'io.reactivex.rxjava2:rxjava:2.0.1'
}
